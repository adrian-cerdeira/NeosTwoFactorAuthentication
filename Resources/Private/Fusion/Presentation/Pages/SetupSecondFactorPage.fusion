prototype(Sandstorm.NeosTwoFactorAuthentication:Page.SetupSecondFactorPage) < prototype(Neos.Fusion:Component) {
    site = null
    styles = ${[]}
    scripts = ${[]}
    username = ''
    flashMessages = ${[]}

    renderer = Neos.Fusion:Join {
        doctype = '<!DOCTYPE html>'
        doctype.@position = 'start 100'

        content = Neos.Fusion:Component {
            @apply.props = ${props}

            backgroundImageSource = Neos.Fusion:Component {
                @if.set = ${this.path}
                path = ${Configuration.setting('Neos.Neos.userInterface.backendLoginForm.backgroundImage')}

                renderer = Neos.Fusion:Case {
                    resourcePath {
                        condition = ${String.indexOf(props.path, 'resource://') == 0}
                        renderer = Neos.Fusion:ResourceUri {
                            path = ${props.path}
                        }
                    }

                    default {
                        condition = true
                        renderer = ${props.path}
                    }
                }
            }
            backgroundImageSourceIsWebp = ${this.backgroundImageSource ? String.endsWith(this.backgroundImageSource, '.webp') : null}

            headerComment = ${Configuration.setting('Neos.Neos.headerComment')}

            renderer = afx`
                <html lang="en">
                {props.headerComment}
                <head>
                    <meta charset="utf-8" />
                    <meta name="robots" content="noindex" />
                    <title>{I18n.translate('login.index.title', 'Login to')} {props.site.name}</title>
                    <Neos.Fusion:Loop items={props.styles}>
                        <link rel="stylesheet" @children="attributes.href">
                            <Neos.Fusion:ResourceUri path={item}/>
                        </link>
                    </Neos.Fusion:Loop>

                    <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/apple-touch-icon.png')} sizes="180x180" rel="apple-touch-icon" />
                    <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/favicon-16x16.png')} sizes="16x16" rel="icon" type="image/png" />
                    <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/favicon-32x32.png')} sizes="32x32" rel="icon" type="image/png" />
                    <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/safari-pinned-tab.svg')} rel="mask-icon" color="#00adee" />

                    <style type="text/css" @if.set={props.backgroundImageSource && !props.backgroundImageSourceIsWebp}>
                        {'body.neos--bg,.neos-login-box:before{background-image:url(' + props.backgroundImageSource + ')}'}
                    </style>
                    <style type="text/css" @if.set={props.backgroundImageSource && props.backgroundImageSourceIsWebp}>
                        {'.webp body.neos--bg,.webp .neos-login-box:before {background-image: url(' + props.backgroundImageSource + ')}'}
                        {'.no-webp body.neos--bg,.no-webp .neos-login-box:before {background-image: url(' + String.substr(props.backgroundImageSource, 0, String.length(props.backgroundImageSource) - 4) + 'jpg)}'}
                    </style>
                </head>
                <body class={["neos", props.backgroundImageSource ? 'neos--bg' : null]} style="">
                    <img @if.set={props.backgroundImageSourceIsWebp} style="display: none" src="data:image/webp;base64,UklGRiIAAABXRUJQVlA4IBYAAAAwAQCdASoBAAEADsD+JaQAA3AAAAAA" onload="document.documentElement.classList.add('webp')" onerror="document.documentElement.classList.add('no-webp')"/>
                    <div class="neos-modal-centered">
                        <main class="neos-login-main">
                            <div class={['neos-login-box', props.backgroundImageSource ? 'background-image-active' : null]}>
                                <figure class="neos-login-box-logo">
                                    <img class="neos-login-box-logo-resource" @children="attributes.src" width="200px" height="200px" alt="Neos Logo">
                                        <Neos.Fusion:ResourceUri path="resource://Neos.Neos/Public/Images/Login/Logo.svg" />
                                    </img>
                                </figure>

                                <h1 class="neos-login-heading">
                                    {I18n.id('login.index.title').value('Login to').package('Neos.Neos').source('Main')} <strong>{site.name}</strong>
                                </h1>

                                <div class="neos-login-body neos">
                                    <Sandstorm.NeosTwoFactorAuthentication:Component.LoginFlashMessages flashMessages={props.flashMessages} />
                                    <Neos.Fusion.Form:Form form.target.action="setupSecondFactor">
                                        <div class="neos-control-group">
                                            <img src={qrCode} style="width: 100%; max-width: 400px"/>
                                        </div>

                                        <div class="neos-two-factor__secret-wrapper">
                                            <Neos.Fusion.Form:Hidden attributes.id="secret" field.name="secret" field.value={secret}/>
                                            <div class="neos-control-group neos-actions">
                                                <button type="button" class="neos-span5 neos-pull-right neos-button neos-login-btn neos-two-factor__secret__show__button">
                                                    {I18n.id('form.secret.show').package('Sandstorm.NeosTwoFactorAuthentication')}
                                                </button>
                                            </div>

                                            <dialog>
                                                <div>
                                                    <div class="neos-two-factor__secret">
                                                        <p>
                                                            {
                                                                Array.join(
                                                                    Array.map(
                                                                        String.split(secret, ''),
                                                                        char => Type.isNumeric(char) ? '<span class="neos-two-factor__secret__number">' + char + '</span>' : '<span>' + char + '</span>'
                                                                    ),
                                                                    ''
                                                                )
                                                            }
                                                        </p>

                                                        <div class="neos-two-factor__secret__overflow-indicator--left">
                                                          <i><svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 320 512"><!--!Font Awesome Free 6.6.0 by @fontawesome - https://fontawesome.com License - https://fontawesome.com/license/free Copyright 2024 Fonticons, Inc.--><path d="M9.4 233.4c-12.5 12.5-12.5 32.8 0 45.3l192 192c12.5 12.5 32.8 12.5 45.3 0s12.5-32.8 0-45.3L77.3 256 246.6 86.6c12.5-12.5 12.5-32.8 0-45.3s-32.8-12.5-45.3 0l-192 192z"/></svg></i>
                                                        </div>
                                                        <div class="neos-two-factor__secret__overflow-indicator--right">
                                                            <i><svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 320 512"><!--!Font Awesome Free 6.6.0 by @fontawesome - https://fontawesome.com License - https://fontawesome.com/license/free Copyright 2024 Fonticons, Inc.--><path d="M310.6 233.4c12.5 12.5 12.5 32.8 0 45.3l-192 192c-12.5 12.5-32.8 12.5-45.3 0s-12.5-32.8 0-45.3L242.7 256 73.4 86.6c-12.5-12.5-12.5-32.8 0-45.3s32.8-12.5 45.3 0l192 192z"/></svg></i>
                                                        </div>
                                                    </div>

                                                    <div class="neos-two-factor__dialog__actions neos-actions">
                                                        <button type="button" class="neos-two-factor__secret__copy__button neos-button neos-login-btn">
                                                            <span class="neos-two-factor__secret__copy__button__icon">
                                                                <i><svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 384 512"><!--!Font Awesome Free 6.6.0 by @fontawesome - https://fontawesome.com License - https://fontawesome.com/license/free Copyright 2024 Fonticons, Inc.--><path d="M280 64l40 0c35.3 0 64 28.7 64 64l0 320c0 35.3-28.7 64-64 64L64 512c-35.3 0-64-28.7-64-64L0 128C0 92.7 28.7 64 64 64l40 0 9.6 0C121 27.5 153.3 0 192 0s71 27.5 78.4 64l9.6 0zM64 112c-8.8 0-16 7.2-16 16l0 320c0 8.8 7.2 16 16 16l256 0c8.8 0 16-7.2 16-16l0-320c0-8.8-7.2-16-16-16l-16 0 0 24c0 13.3-10.7 24-24 24l-88 0-88 0c-13.3 0-24-10.7-24-24l0-24-16 0zm128-8a24 24 0 1 0 0-48 24 24 0 1 0 0 48z"/></svg></i>
                                                            </span>
                                                            <span class="neos-two-factor__secret__copy__button__icon neos-two-factor__hidden">
                                                                <i><svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 448 512"><!--!Font Awesome Free 6.6.0 by @fontawesome - https://fontawesome.com License - https://fontawesome.com/license/free Copyright 2024 Fonticons, Inc.--><path d="M438.6 105.4c12.5 12.5 12.5 32.8 0 45.3l-256 256c-12.5 12.5-32.8 12.5-45.3 0l-128-128c-12.5-12.5-12.5-32.8 0-45.3s32.8-12.5 45.3 0L160 338.7 393.4 105.4c12.5-12.5 32.8-12.5 45.3 0z"/></svg></i>
                                                            </span>
                                                            {I18n.id('form.secret.copy').package('Sandstorm.NeosTwoFactorAuthentication')}
                                                        </button>
                                                        <button type="button" class="neos-two-factor__secret__close__button neos-button neos-login-btn">
                                                            {I18n.id('form.secret.close').package('Sandstorm.NeosTwoFactorAuthentication')}
                                                        </button>
                                                    </div>
                                                </div>
                                            </dialog>
                                        </div>

                                        <div class="neos-control-group">
                                            <Neos.Fusion.Form:Input
                                                field.name="secondFactorFromApp"
                                                attributes.required="required"
                                                attributes.id="secondFactorFromApp"
                                                attributes.placeholder={I18n.id('otp-placeholder').package('Sandstorm.NeosTwoFactorAuthentication')}
                                                attributes.class="neos-span5"
                                                attributes.aria-label={I18n.id('otp-placeholder').package('Sandstorm.NeosTwoFactorAuthentication')}
                                                attributes.autocomplete="off"
                                            />
                                        </div>

                                        <div class="neos-control-group neos-actions">
                                            <Neos.Fusion.Form:Button attributes.class="neos-span5 neos-pull-right neos-button neos-login-btn">
                                                {I18n.id('module.new.submit-otp').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}
                                            </Neos.Fusion.Form:Button>
                                        </div>
                                    </Neos.Fusion.Form:Form>
                                </div>
                            </div>
                        </main>
                        <footer class="neos-login-footer">
                            <p>
                                <a href="http://neos.io" target="_blank">Neos</a> –&nbsp;
                                © 2006-{Date.format('now', 'Y')} This is free software, licensed under GPL3 or higher, and you are welcome to redistribute it under certain conditions;&nbsp;
                                Neos comes with ABSOLUTELY NO WARRANTY;&nbsp;
                                See <a href="http://neos.io" target="_blank">neos.io</a> for more details. Obstructing the appearance of this notice is prohibited by law.
                            </p>
                        </footer>
                    </div>

                    <Neos.Fusion:Loop items={props.scripts}>
                        <script @children="attributes.src">
                          <Neos.Fusion:ResourceUri path={item}/>
                        </script>
                    </Neos.Fusion:Loop>
                </body>
                </html>
            `
        }
    }
}
