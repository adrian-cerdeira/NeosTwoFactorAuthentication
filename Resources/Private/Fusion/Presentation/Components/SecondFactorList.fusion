prototype(Sandstorm.NeosTwoFactorAuthentication:Component.SecondFactorList) < prototype(Neos.Fusion:Component) {
    factorsAndPerson = ''

    renderer = afx`
        <table class="neos-table">
            <thead>
            <tr>
                <th>{I18n.id('module.index.list.header.name').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}</th>
                <th>{I18n.id('module.index.list.header.type').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}</th>
                <th>&nbsp;</th>
            </tr>
            </thead>
            <tbody>
            <Neos.Fusion:Loop
                items={props.factorsAndPerson}
                itemName="factorAndPerson"
                @children="itemRenderer"
                iterationName="iterator"
            >
                <Sandstorm.NeosTwoFactorAuthentication:Component.SecondFactorList.Entry
                    factorAndPerson={factorAndPerson}
                    iterator={iterator}
                />
            </Neos.Fusion:Loop>
            </tbody>
        </table>
    `
}

prototype(Sandstorm.NeosTwoFactorAuthentication:Component.SecondFactorList.Entry) < prototype(Neos.Fusion:Component) {
    factorAndPerson = ''
    iterator = ''

    editUri = Neos.Fusion:UriBuilder {
        action = 'edit'
        arguments = Neos.Fusion:DataStructure {
            secondFactor = ${factorAndPerson.secondFactor}
        }
    }

    renderer = afx`
        <tr>
            <td>{props.factorAndPerson.user.name.fullName} ({props.factorAndPerson.secondFactor.account.accountIdentifier})</td>
            <td>{props.factorAndPerson.secondFactor.typeAsName}</td>
            <td>
                <button class="neos-button neos-button-danger" data-toggle="modal"
                        href={'#user-' + props.iterator.index} title={I18n.id('module.index.list.action.delete').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()} data-neos-toggle="tooltip">
                    <i class="fas fa-trash-alt icon-white"></i>
                </button>

                <div class="neos-hide" id={'user-' + props.iterator.index}>
                    <div class="neos-modal-centered">
                        <div class="neos-modal-content">
                            <div class="neos-modal-header">
                                <button type="button" class="neos-close neos-button" data-dismiss="modal"></button>
                                <div class="neos-header">
                                    {I18n.id('module.index.delete.header').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}
                                </div>
                                <div>
                                    <div class="neos-subheader">
                                        <p>
                                            {I18n.id('module.index.delete.text').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}
                                        </p>
                                    </div>
                                </div>
                            </div>
                            <div class="neos-modal-footer">
                                <a href="#" class="neos-button" data-dismiss="modal">
                                    {I18n.id('module.index.delete.cancel').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}
                                </a>
                                <Neos.Fusion.Form:Form form.target.action="delete" attributes.class="neos-inline" form.target.arguments={{secondFactor: props.factorAndPerson.secondFactor}}>
                                    <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-danger">
                                        {I18n.id('module.index.delete.confirm').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}
                                    </Neos.Fusion.Form:Button>
                                </Neos.Fusion.Form:Form>
                            </div>
                        </div>
                    </div>
                    <div class="neos-modal-backdrop neos-in"></div>
                </div>
            </td>
        </tr>
    `
}
